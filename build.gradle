/*
 * Copyright (c) 2018-2020 William Frank (info@williamfrank.net)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
    id 'signing'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

version = '0.3.1-SNAPSHOT'
group = 'io.github.tri-omega'
archivesBaseName = 'typescript-service-generator'

repositories {
    mavenLocal()
    mavenCentral()
}

ext.versions = [
        spring        : '7.0.2',
        guava         : '33.5.0-jre',
        junit         : '5.6.2',
        lombok        : '1.18.42',
        compileTesting: '0.23.0',
]

idea {
    module {
        // Marks the already(!) added srcDir as "generated"
        generatedSourceDirs += file("${projectDir}/src/test/resources")
    }
}

sourceSets {
    test {
        resources {
            srcDirs = ["${projectDir}/src/test/java", "${projectDir}/src/test/resources"]
            include '**/*'
        }
    }
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:${versions.lombok}"

    compileOnly("org.springframework:spring-webmvc:${versions.spring}")
    compileOnly "org.projectlombok:lombok:${versions.lombok}"

    testImplementation "com.google.guava:guava:${versions.guava}"
    testImplementation "com.google.testing.compile:compile-testing:${versions.compileTesting}"

    testImplementation("org.junit.jupiter:junit-jupiter-api:${versions.junit}")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:${versions.junit}")

    testImplementation("org.springframework:spring-webmvc:${versions.spring}")

    testImplementation "org.projectlombok:lombok:${versions.lombok}"
    testAnnotationProcessor "org.projectlombok:lombok:${versions.lombok}"
}

test {
    useJUnitPlatform()
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

tasks.withType(Javadoc).configureEach {
    options.addBooleanOption('Xdoclint:none', true)
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

ext.uploadUserName = project.hasProperty('ossrhUsername') ? ossrhUsername : ""
ext.uploadPassword = project.hasProperty('ossrhPassword') ? ossrhPassword : ""

publishing {
    repositories {
        maven {
            def releasesRepoUrl = layout.buildDirectory.dir('https://oss.sonatype.org/service/local/staging/deploy/maven2/')
            def snapshotsRepoUrl = layout.buildDirectory.dir('https://oss.sonatype.org/content/repositories/snapshots/')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = uploadUserName
                password = uploadPassword
            }
        }
    }

    publications {
        archive(MavenPublication) {
            from components.java
            artifact source: sourcesJar, classifier: 'sources', extension: 'zip'
            artifact source: javadocJar, classifier: 'javadoc', extension: 'zip'
//            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            pom {
                name = 'TypeScript Service Generator'
                packaging = 'jar'

                description = 'Compile time library to auto-generate TypeScript services for Spring MVC controllers and ' +
                        'interfaces for all the return and parameter types to be used in Angular client side projects'
                url = 'https://tri-omega.github.io/typescript-service-generator/'

                scm {
                    connection = 'scm:git:git://github.com/tri-omega/typescript-service-generator.git'
                    developerConnection = 'scm:git:ssh://github.com:tri-omega/typescript-service-generator.git'
                    url = 'https://github.com/tri-omega/typescript-service-generator.git'
                }

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'william.frank'
                        name = 'William Frank'
                        email = 'info@williamfrank.net'
                    }
                }
            }
        }
    }
}
